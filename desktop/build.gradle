//Java版本
sourceCompatibility = 1.8
//Java代码目录
sourceSets.main.java.srcDirs = ["src/"]
//指定资源目录
sourceSets.main.resources.srcDirs = ["../assets"]

/**
 * 自定义字段
 * mainClassName: 启动类,一般不需要变
 * workingDir: 工作空间,如果是idea启动,会帮你设置好,但是如果执行task,则需要自己指定到对应工作目录
 */
project.ext.mainClassName = "com.rock.pokemon.gdx.DesktopLauncher"
project.ext.workingDir = "/Users/ayl/work/Pokemon/"

import org.gradle.internal.os.OperatingSystem

/**
 * 执行
 */
task run(dependsOn: classes, type: JavaExec) {

    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    workingDir = project.workingDir
    standardInput = System.in
    ignoreExitValue = true

    /**
     * 如果系统是MAC
     */
    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }

}

/**
 * 调试
 */
task debug(dependsOn: classes, type: JavaExec) {

    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    workingDir = project.workingDir
    standardInput = System.in
    ignoreExitValue = true
    debug = true

    /**
     * 如果系统是MAC
     */
    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }

}

task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}


dist.dependsOn classes

eclipse.project.name = appName + "-desktop"
